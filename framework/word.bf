[
operations to words that are type independent

use "helpers/test_helpers.bf"
]

>>>>>>

move_left{
    = 0 0 0 0 | 0 0 | `A B C D
    [<<<<<< + >>>>>> -]>
    [<<<<<< + >>>>>> -]>
    [<<<<<< + >>>>>> -]>
    [<<<<<< + >>>>>> -]
    <<<<<<<<<
    = `A B C D | 0 0 | 0 0 0 0
}

= ~

TEST can move word left
>>>>>> +++ > +++++ > +++++++ > +++++++++ <<<
= 0 0 0 0 | 0 0 | `3 5 7 9
move_left{ [<<<<<<+>>>>>>-]>[<<<<<<+>>>>>>-]>[<<<<<<+>>>>>>-]>[<<<<<<+>>>>>>-]<<<<<<<<< }
= `3 5 7 9 | 0 0 | 0 0 0 0
[-]>[-]>[-]>[-]<<<

move_right{
    = `A B C D | 0 0 | 0 0 0 0
    [>>>>>> + <<<<<< -]>
    [>>>>>> + <<<<<< -]>
    [>>>>>> + <<<<<< -]>
    [>>>>>> + <<<<<< -]
    >>>
    = 0 0 0 0 | 0 0 | `A B C D
}

= ~

TEST can move word right
+++ > +++++ > +++++++ > +++++++++ <<<
= `3 5 7 9 | 0 0 | 0 0 0 0
move_right{ [>>>>>>+<<<<<<-]>[>>>>>>+<<<<<<-]>[>>>>>>+<<<<<<-]>[>>>>>>+<<<<<<-]>>> }
= 0 0 0 0 | 0 0 | `3 5 7 9
[-]>[-]>[-]>[-]

= ~
>>>>>> >>>>>> >>>>>> >>>>>> >>>>>> >>
= 0 0 | 0 0 0 0 | 0 0 | 0 0 0 0 | 0 0 | 0 0 0 0 | 0 0 | `A0 A1 A2 A3 |

takes a word argument and copies it to the right using the padding
copy_right{
    = `A B C D | 0 X | 0 0 0 0
    [>>>>+>>+<<<<<<-]>>>>[<<<<+>>>>-]<<<
    [>>>+>>>+<<<<<<-]>>>[<<<+>>>-]<<
    [>>+>>>>+<<<<<<-]>>[<<+>>-]<
    [>+>>>>>+<<<<<<-]>[<+>-]
    <<<<
    = `A B C D | 0 X | A B C D
}

= ~

TEST can copy word right
+++ > +++++ > +++++++ > +++++++++ >> ++ <<<<<
= `3 5 7 9 | 0 2 | 0 0 0 0
copy_right{
    [>>>>+>>+<<<<<<-]>>>>[<<<<+>>>>-]<<<[>>>+>>>+<<<<<<-]>>>[<<<+>>>-]<<[>>+>>>>+<<<
    <<<-]>>[<<+>>-]<[>+>>>>>+<<<<<<-]>[<+>-]<<<<
}
= `3 5 7 9 | 0 2 | 3 5 7 9
[-]>[-]>[-]>[-]>>>[-]>[-]>[-]>[-]<<< <<<<<<

takes a word argument and copies it to the left using the padding
copy_left{
    = 0 0 0 0 | 0 X | `A B C D
    [<<+<<<<+>>>>>>-]<<[>>+<<-]>>>
    [<<<+<<<+>>>>>>-]<<<[>>>+<<<-]>>>>
    [<<<<+<<+>>>>>>-]<<<<[>>>>+<<<<-]>>>>>
    [<<<<<+<+>>>>>>-]<<<<<[>>>>>+<<<<<-]
    >>
    =  A B C D | 0 X | `A B C D
}

= ~

TEST can copy word left
>>>>>> +++ > +++++ > +++++++ > +++++++++ >> ++ <<<<<
= 0 0 0 0 | 0 2 | `3 5 7 9
copy_left{
    [<<+<<<<+>>>>>>-]<<[>>+<<-]>>>[<<<+<<<+>>>>>>-]<<<[>>>+<<<-]>>>>[<<<<+<<+>>>>>>-
    ]<<<<[>>>>+<<<<-]>>>>>[<<<<<+<+>>>>>>-]<<<<<[>>>>>+<<<<<-]>>
}
= 3 5 7 9 | 0 2 | `3 5 7 9
<<<<<<[-]>[-]>[-]>[-]>>>[-]>[-]>[-]>[-]

= ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }

TEST: 0 eq 0
= `0 0 0 0
eq_0{ [>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<< }
= `1 0 0 0
[-]>[-]>[-]>[-]<<<

TEST: 0 0 0 1 ne 0
>>>+<<<
= `0 0 0 1
eq_0{ [>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<< }
= `0 0 0 0
[-]>[-]>[-]>[-]<<<

TEST: 0 0 8 0 ne 0
>>++++++++<<
= `0 0 8 0
eq_0{ [>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<< }
= `0 0 0 0
[-]>[-]>[-]>[-]<<<

TEST: 0 2 0 1 ne 0
>++>>+<<<
= `0 2 0 1
eq_0{ [>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<< }
= `0 0 0 0
[-]>[-]>[-]>[-]<<<

TEST: 255 2 3 255 ne 0
->++>+++>-<<<
= `255 2 3 255
eq_0{ [>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<< }
= `0 0 0 0
[-]>[-]>[-]>[-]<<<

Returns 1 if two words are exactly the same; does not touch padding
eq{
    = `A B C D | p0 p1 | W X Y Z
    >>>>>>
    = A B C D | p0 p1 | `W X Y Z
    subtract W from A and so on
    [<<<<<< - >>>>>> -]>
    = A_diff B C D | p0 p1 | 0 `X Y Z
    [<<<<<< - >>>>>> -]>
    [<<<<<< - >>>>>> -]>
    [<<<<<< - >>>>>> -]
    <<<<<< <<<
    = `A_diff B_diff C_diff D_diff | p0 p1 | 0 0 0 0
    eq_0{ [>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<< }
    = `is_eq 0 0 0 | p0 p1 | 0 0 0 0
}

= ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }

TEST: 0 0 0 0 eq 0 0 0 0
= `0 0 0 0 | 0 0 | 0 0 0 0
eq{
    >>>>>>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]<<<<<<<
    <<[>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<<
}
= `1 0 0 0 | 0 0 | 0 0 0 0 ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }

TEST: 0 0 0 1 ne 0 0 0 0
>>>+<<<
= `0 0 0 1 | 0 0 | 0 0 0 0
eq{
    >>>>>>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]<<<<<<<
    <<[>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<<
}
= `0 0 0 0 | 0 0 | 0 0 0 0 ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }

TEST: 0 0 0 0 ne 0 0 0 1
>>>>>> >>> + <<< <<<<<<
= `0 0 0 0 | 0 0 | 0 0 0 1
eq{
    >>>>>>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]<<<<<<<
    <<[>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<<
}
= `0 0 0 0 | 0 0 | 0 0 0 0 ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }

TEST: 2 0 8 3 eq 2 0 8 3
++>>++++++++>+++>>>++>>++++++++>+++<<<<<< <<<
= `2 0 8 3 | 0 0 | 2 0 8 3
eq{
    >>>>>>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]>[<<<<<<->>>>>>-]<<<<<<<
    <<[>>>[-]+<<<[-]]+>[>>[-]+<<[-]]>[>[-]+<[-]]>[<<<[-]>>>[-]]<<<
}
= `1 0 0 0 | 0 0 | 0 0 0 0 ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }

= ~
test_helpers/clear{ [-]->[-]>[-]>[-]++++++[<++++++[<++++++>-]>-]<<[>[-]<[>+<-]>-]+[[-]<+] }
>>>>>> >>>>>>

Takes a word argument and inflates it into the left four left padding slots
inflate_left{
    = 0 * | * * * * | 0 * | * * * * | 0 * | * * * * | 0 * | `A0 A1 A2 A3
    [<<<<<< <<<<<< <<<<<< << + >>>>>> >>>>>> >>>>>> >> -]
    = A0 * | * * * * | 0 * | * * * * | 0 * | * * * * | 0 * | `0 A1 A2 A3 | 0 0
    >[<<<<<< <<<<<< <<< + >>>>>> >>>>>> >>> -]
    = A0 * | * * * * | A1 * | * * * * | 0 * | * * * * | 0 * | 0 `0 A2 A3 | 0 0
    >[<<<<<< <<<< + >>>>>> >>>> -]
    = A0 * | * * * * | A1 * | * * * * | A2 * | * * * * | 0 * | 0 0 `0 A3 | 0 0
    >[<<<<< + >>>>> -]
    = A0 * | * * * * | A1 * | * * * * | A2 * | * * * * | A3 * | 0 0 0 `0 | 0 0
    <<<
}
= A0 * | * * * * | A1 * | * * * * | A2 * | * * * * | A3 * | `0 0 0 0 | 0 0
= ~

TEST: can inflate left
+ > ++ > +++ > ++++ <<<
= 0 * | * * * * | 0 * | * * * * | 0 * | * * * * | 0 * | `1 2 3 4 | 0 0
inflate_left{
    [<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>-]>[<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>-]>
    [<<<<<<<<<<+>>>>>>>>>>-]>[<<<<<+>>>>>-]<<<
}
= 1 * | * * * * | 2 * | * * * * | 3 * | * * * * | 4 * | `0 0 0 0 | 0 0
<<[-]<<<<<<[-]<<<<<<[-]<<<<<<[-]
